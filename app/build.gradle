apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.yuexiaohome.tempalarm"
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 2
        versionName "1.01"

        // dex突破65535的限制
        multiDexEnabled true

        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }


    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    signingConfigs {
        debug {
            // No debug config
        }
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "_debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
//                        def fileName = outputFile.name
//                                .replace(".apk", "-${variant.versionName}(${variant.versionCode}).apk")
                        def fileName="${rootProject.name}_v${variant.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk";

                        // 输出apk名称为boohee_v1.0_2015-01-15_wandoujia.apk
                        //def fileName = "boohee_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }

        }
    }

    // 友盟多渠道打包
    productFlavors {
        //wandoujia {}
        fir{}
        //_360 {}
        //baidu {}
        //xiaomi {}
        //tencent {}
        //taobao {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    dexOptions {
        preDexLibraries = false
    }
}

assembleRelease << {
    print "Copying APK to root directory..."
    copy {
        from 'build/outputs/apk'
        into project.rootDir
        exclude '*unaligned*', '*debug*'
        rename {
            String fileName -> fileName.replace('app', rootProject.name)
        }
    }
    println "done"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
}